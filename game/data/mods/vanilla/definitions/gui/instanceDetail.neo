Gui: gInstanceDetail {
	
	template = Vertical;
	
	Script: new(instance) {
		->instance = instance;
	    ->background = "gui/back_stone01.png";
		->textColor = [250, 200, 180];
		//->margin = [10, 10, 10, 10];
		->padding = [20, 30, 40, 50];
		//->absX = 100;
		//->absY = 60;
		
		->instanceList = [];
		->attributeList = [];
		->c = -1;
		
		update();
	}
	
	Script: tick() {
		if (! ->instance) {
			//clearGui();
			destroy(self);
			return;
		}
	
		update();
	}
	
	Script: update() {
		->text = getName(->instance);
		
		// Adding guis for new instances
		if (->c++ % 100 == 0) {
	
			for(attribute : eachAttribute(->instance)) {
				found = false;
				for (a : ->attributeList) {
					if (getAttId(a->attribute) == getAttId(attribute)) {
						found = true;
						break;
					}
				}
				if (!found) {
					->attributeList += addGui(self, gAttribute, attribute, ->instance);
				}
			}
			
			for(subInstance : getItems(->instance)) {
				found = false;
				for (i : ->instanceList) {
					if (subInstance == i->instance) {
						found = true;
						break;
					}
				}
				if (!found) {
					->instanceList += addGui(self, gSubInstance, subInstance);
				}
			}
			
		}
	}
}

Gui: gText {

	template = Vertical;
	
	Script: new(text) {
		->text = text;
	}
}

Gui: gSubInstance {
	template = Vertical;
	
	Script: new(i) {
		->instance = i;
		if (! ->instance) {
			destroy(self);
			return;
		}
	
		->padding = [0, 30, 0, 30];
		
		->parent = getHolder(->instance);
		->background = "gui/back_wood01.png";
		->text = getName(->instance);
	}
	
	Script: tick() {
		if (! ->instance || getHolder(->instance) != ->parent) {
			destroy(self);
		}
	}
	
	Script: click() {
		clearGui();
		createGui(gInstanceDetail, ->instance);
	}
}

Gui: gAttribute {

	template = Vertical;
	
	Script: new(attribute, instance) {
		->attribute = attribute;
		->instance = instance;
		->textColor = [200, 240, 180];
		
		->text = attribute;
	}
	
	Script: tick() {
		if (! ->instance) {
			destroy(self);
			return;
		}
	
		value = getAttValue(->instance, getAttId(->attribute));
		->text = value;
		if (value == 0) { destroy(self); }
	}
}

Gui: gVariables {

	template = Vertical;
	
	Script: new(target) {
		->target = target;
	}
	
	Script: tick() {
		clearGui(self);
		for (var : eachVariable(->target)) {
			addGui(self, gText, var);
		}
	}

}
