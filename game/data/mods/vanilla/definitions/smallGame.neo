///////////////////////////////////////////////////////////////////////////// Tiles

Tile: tDirt {
	name = "Dirt";

	preferredHeight = 128,5;

	topColor {
		200,10;
		150;
		30,10;
	}
	sideColor {
		120,10;
		110,10;
		90;
	}

	Script: tick {
		if (chance(0.01)) {
			change(self,tGras);
		}
		print ("Dirty Test");
	}
}

Tile: tGras {
	name = "Gras";
	preferredHeight = 180;
	preferredHeightBlur = 20;

	topColor {
		120,10;
		200,15;
		50;
	}
	sideColor {
		100,10;
		90,10;
		80;
	}

	attributes {
		attNutrition, 3;
		attHumidity, 50;
	}
}

Tile: tStone {
	preferredHeight = 240;preferredHeightBlur = 10;
	name = "Stone"; topColor {150,10;150;120;}
}

///////////////////////////////////////////////////////////////////////////// Attributes
Attribute: attLife { name = "Life"; }
Attribute: attViewingDistance { name = "Viewing Distance"; }
Attribute: attNutrition { name = "Nutrition"; }
Attribute: attEdible { name = "Edible"; }
Attribute: attLightLevel { name = "Light Level"; }
Attribute: attHumidity { name = "Humidity"; }

///////////////////////////////////////////////////////////////////////////// Creatures

Creature: cMonkey {
	name = "Monkey";
	mesh = "monkey.obj";

	knowledge {
		proStroll;
		proEat;
		proGatherFood;
		proPickUpFood;
	}

	drives {
		driHunger;
		driMove;
	}

	attributes {
		attLife, 10;
		attSpeed, 3;
		attViewingDistance, 7;
		attStepHeight, 7;
	}

	Script: place {
		self->id = random(1000);
		self->lastmove = random(3);
		.attNutrition += random(5,10);
	}

	Script: tick {
		if (chance(0.125)) {
			.attNutrition--;
		}

		if (..attNutrition < 0) {
			destroy(self);
		}
	}
}

Drive: driMove {
	Script: condition {
		->lastmove++;
		if (->lastmove > 3) { ->lastmove = 0; }
		require(->lastmove == 0);
	}

	solutions {
		proStroll;
	}
}

Process: proStroll {
	Script: process {
		if (getHeight(tile = getNeighbor(getTile(self), random(3))) >= 100) {
		    print("---------------------------- " + ->lastmove + "   from " + getTile(self) + " going to " + tile);
		    moveTo(self,tile);
		}
	}
	
	//solutions {}
	
	//Script: discovery {}
}

Drive: driHunger {
	Script: condition {
		require(..attNutrition <= 10);
	}

	solutions {
		proEat;
	}
}

Process: proEat {
	Script: condition {
		require(getItemAtt(self, "attEdible"));
	}
	Script: process {
		item = getItemAtt(self, "attEdible");
		item->consume(self);
	}

	solutions {
		proPickUpFood;
		proGatherFood;
	}
}

Process: proPickUpFood {
	Script: condition {
	    ->food = getAttInRange("attEdible",self,0);
		require(->food);
	}
	Script: process() {
		pickUp(self, ->food);
	}
	solutions {
	}
}

Process: proGatherFood {
	Script: condition {
		->food = getAttInRange("attEdible",self,..attViewingDistance);
		require(->food);
	}
	Script: process() {
		moveTowards(self, ->food, ..attSpeed);
	}
	Script: discovery() {
		require(..attSpeed > 0);
		require(..attViewingDistance > 0);
	}
}

///////////////////////////////////////////////////////////////////////////// Items

Entity: entFruit {
	name = "Apple";
	mesh = "apple02.ply";

	attributes {
		attNutrition, 5;
		attEdible, 1;
	}

	Script: tick() {
		if (chance(0.02)) {
			destroy(self);
		}
	}

	Script: consume(actor) {
		//addPersonalAtt(actor, "attNutrition", ..attNutrition);
		actor.attNutrition += ..attNutrition;
		destroy(self);
	}
}

///////////////////////////////////////////////////////////////////////////// Plants

Creature: cTree {
	name = "Tree";
	mesh = "tree01.ply";

	drives {
		driPlantReproduction;
	}

	knowledge {
		proPhotoSynthesis;
		proCreateFruit;
	}

	Script: place {
		.attNutrition += random(30);
	}

	attributes{attLightLevel,50;}
}

Drive: driPlantReproduction {
	solutions {
		proCreateFruit;
		proPhotoSynthesis;
	}
}

Process: proPhotoSynthesis {
	Script: condition {
		require(getLightLevel(self) + ..attLightLevel >= 50);
		//require(getTile(self).attHumidity >= 20);
	}
	Script: process {
		//getTile(self)->attHumidity -= 5;
		.attNutrition += 1;
	}

	solutions {} // there is no way for a plant to create light (currently?)

	Script: discovery(actor) { // there is no way for an entity to learn photosynthesisis
		require(0);
	}
}

Process: proCreateFruit {
	Script: condition {
		require(chance(0.1));
		require(!getAttInRange("attEdible", self, 1));
		require(..attNutrition >= 30);
	}

	Script: process {
		fruit = create(entFruit, getTile(self));
		.attNutrition -= 30;
		//pickUp(self, fruit);
	}
}
