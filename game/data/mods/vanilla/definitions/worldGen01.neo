WorldGen: genContinental {
	name = "Continental";

	Script: generate {
		setWaterLevel(100);

		for (t : eachTile()) {
			if (chance(0.0025)) { // 1/4% chance
				if (chance(0.75)) {
					create(formPlateau, t);
				} else {
					create(formHill, t);
				}
			}
		}

		for (t : eachTile()) {
			if (getHeight(t) > 100) {
				if (chance(0.01)) {
					monkey = create(cMonkey, t);
				}
				if (chance(0.01)) {
					fruit = create(entFruit, t);
				}
			}
			if (getHeight(t) > 150) {
				if (chance(0.2)){
					create(cTree,t);
				}
			}
		}

		fitTiles();
	}
}

Formation: formHill {
	Script: place(tile) {
		height = random(150,200);
		self->lift(tile, height);
	}

	Script: lift(tile, height) {
		if (height > getHeight(tile)) {
			setHeight(tile, height);

			for (n : getNeighbors(tile)) {
				deviation = random(1,12);
				lift(n, height - deviation);
			}
		}
	}
}

Formation: formPlateau {
	Script: place(tile) {
		height = random(110,230);
		if (height >= 170) { height = random(110,230);}
		distance = random(12,20);
		self->lift(tile, height, distance);
	}

	Script: lift(tile, height, distance) {
		if (chance(0.1)) {
			->lift(tile, height + random(5,15), distance / 2);
		} else if (height > getHeight(tile)) {
			setHeight(tile, height);

			for (i=0; i<3; i++) {
				if (distance - random(0,7) >= 0) {
					deviation = random(-1,3);
				} else {
					deviation = random(30,50);
				}
				lift(getNeighbor(tile, i), height - deviation, distance - 1);
			}
		}
	}
}
