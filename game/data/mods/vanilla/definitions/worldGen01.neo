WorldGen: genContinental {
	name = "Continental";

	Script: generate {
		setWaterLevel(100);

		for (t : eachTile) {
			if (chance(0.01)) { // 1% chance
				if (chance(0.5)) {
					create("formPlateau", t);
				} else {
					create("formHill", t);
				}
			}
		}

		for (t : eachTile) {
			if (getHeight(t) > 100) {
				if (chance(0.05)) {
					create("cTree", t);
				} else if (chance(0.01)) {
					create("cMonkey", t);
				}
			}
		}

		fitTiles();
	}
}

Formation: formHill {
	Script: place(tile) {
		height = random(110,150);
		self->lift(tile, height);
	}

	Script: lift(tile, height) {
		if (height > getHeight(tile)) {
			setHeight(tile, height);

			for (i=0; i<3; i++) {
				deviation = random(1,12);
				lift(getNeighbor(tile, i), height - deviation);
			}
		}
	}
}

Formation: formPlateau {
	Script: place(tile) {
		height = random(120,200);
		distance = random(2,12);
		self->lift(tile, height, distance);
	}

	Script: lift(tile, height, distance) {
		if (chance(0.1)) {
			->lift(tile, height + random(5,15), distance);
		} else if (height > getHeight(tile)) {
			setHeight(tile, height);

			for (i=0; i<3; i++) {
				if (distance - random(0,7) >= 0) {
					deviation = random(-1,3);
				} else {
					deviation = random(20,42);
				}
				lift(getNeighbor(tile, i), height - deviation, distance - 1);
			}
		}
	}
}
