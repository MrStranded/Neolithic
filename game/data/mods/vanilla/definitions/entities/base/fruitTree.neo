Creature: cFruitTree {

	drives {
		driSoak;
		driGatherEnergy;
		driGrow;
		driDeathwish;
		driPlantReproduction;
	}

	knowledge {
		proSoak;
		proPhotosynthesis;
		proGrow;
		proDie;
	}

	attributes{
	    attMaxHealth, 100;
	    attTicks, 2;
		attMaxSize, 50;
		attEfficiency, 50;
	    attMatureAge, 420;//2880;
	    attLightSensitivity, 20;
	}
	
	Script: new {
        for (n : getNeighbors(self)) {
            effect = addEffect(n, effShadow);
            effect->cause = self;
        }
	}
	
	Script: tick {
	    delayNextTick(self, .attTicks);
	    ..attAge++;

	    if (getWaterHeight(self) > getHeight(self)) {
	        ..attDamage++;
	    }

		if (.attAge > .attMatureAge * 3 && ! hasEffect(self, effCancer)) { // age death
		    addEffect(self, effCancer);
        }
		
		if (.attWater <= 0 && ! hasEffect(self, effDry)) {
		    addEffect(self, effDry);
        }
	}
	
	Stage: ripe {
		drives {
			driSoak;
			driGatherEnergy;
			driGrow;
			driDeathwish;
			driPlantReproduction;
		}
	}
	
	Stage: sapling {	
		drives {
			driSoak;
			driGatherEnergy;
			driGrow;
		}
	
		attributes{
			attMaxHealth, 50;
		}
		
		Script: tick {
			print("sapling tick");
			delayNextTick(self, .attTicks);
			..attAge++;

			if (getWaterHeight(self) > getHeight(self)) {
				..attDamage++;
			}
			
			if (.attWater <= 0 && ! hasEffect(self, effDry)) {
				addEffect(self, effDry);
			}
		
			if (.attSize >= .attMaxSize / 2) {
				setDefaultStage(self);
			}
		}
	}
	
    Stage: seed {
        drives { driGrowIntoTree; }
        knowledge { proGrowIntoTree; }
		
		attributes {
			attSize, 1;
		}

        Script: tick() {
            if (isOnFloor(self) && chance(0.001)) {
                destroy(self);
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////// Shadow

Effect: effShadow {
    name = "Shadow";

    attributes {
        attLightLevel, "-10";
    }
	
	Script: tick() {
		..attLightLevel = - (->cause..attSize) / 3;
	}

    Script: removeCondition() {
        require(! ->cause);
    }
}

///////////////////////////////////////////////////////////////////////////// Drought

Effect: effDry {
    name = "Dry";
	
	Script: tick(carrier) {
		if (carrier.attWater <= 0) {
			if (..attDamage < carrier.attSize && chance(0.01)) {
				..attDamage++;
			}
		} else {
			..attDamage -= 10;
			carrier..attWater --;
		}
	}

    Script: removeCondition() {
        require(.attDamage <= 0 || carrier.attWater > 0);
    }
}
