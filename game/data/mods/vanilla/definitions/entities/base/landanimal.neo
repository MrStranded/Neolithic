
///////////////////////////////////////////////////////////////////////////// land animal

Creature: cLandAnimal {
	name = "Land animal";

	knowledge {
	    proDrink; proSearchWater;
		proMate; proSearchMate;
		proSleep;
		proStroll;
	}

	drives {
	    driThirst;
		driHunger;
		driMate;
		driSleep;
		driMove;
		driNotSitOnShit;
	}

	attributes {
	    attMeat, 1;
	}

	Script: place {
        ..attGender = random(0,2)*2 - 1; // this results in either -1 or 1
        ->updateMesh();
	}

	Script: tick {
	    if (..attNutrition > 0) {
	        if (! .attSleeping) {
                ..attNutrition--;
            } else if (chance(0.1)) {
                ..attNutrition--;
            }
        }

	    if (! .attSleeping) { // creature is awake
            ..attFatigue++;
		} else {
		    if (..attFatigue > 0) {
                ..attFatigue--;
            }
		}

		if (.attAge >= .attMatureAge) { // age death
		    if (chance(0.01) && !getEffects(self, effCancer)) {
                addEffect(self, effCancer);
		    }
        }

        ..attAge += 1;

		if (.attHealth <= 0) { // death
		    print(self + " died from death");
		    carcass = create(entCarcass, getTile(self));
		    carcass..attNutrition = .attNutrition;
		    destroy(self);
        }
	}

	Script: reachable (from, to) {
        h1 = getHeight(from);
        h2 = getHeight(to);
        hw = getWaterHeight(to);
		freeSpace = 100 - getFullAtt("attSize", to);
        return ((h2 >= hw - .attStepHeight) && (abs(h1-h2) <= .attStepHeight) && freeSpace >= .attSize);
	}

}

///////////////////////////////////////////////////////////////////////////// carcass

Entity: entCarcass {
    name = "Carcass";
    mesh = "carcass_medium.ply";

    attributes {
        attMeat, 1;
        attNutrition, 100; // default value
    }

    Script: tick {
        if (chance(0.1)) {
            ..attNutrition--;
        }

        if (.attNutrition <= 0) {
            destroy(self);
        }
    }

	Script: consume(actor) {
	    print(actor + " consumes carcass");
	    effect = addEffect(actor, effDigest);
        effect..attNutrition = .attNutrition;
        for (item : getItems(self)) {
            pickUp(effect, item);
        }
		destroy(self);
	}
}

///////////////////////////////////////////////////////////////////////////// Effects

Effect: effCancer {
    Script:tick(carrier) {
        if (chance(0.01)) {
            ..attHealth--;
            if (..attHealth == -20) {
                ..attSpeed--;
            }
        }
    }

    Script: removeCondition {
        return(false);
    }
}