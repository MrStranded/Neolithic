Entity: entSource {
    name = "Sweet water source";

    Script: tick {
        water = getInstance(entWater, getTile(self));

        if (water) {
            water..attWater++;
        } else {
            (create(entWater, getTile(self)))..attWater++;
        }
    }
}

Entity: entWater {
    name = "Sweet water";

    Script: tick {
        if (chance(0.01)) {
            ..attWater--;
        }

        h = getHeight(self) + ..attWater;

        if (..attWater > 0) {
            lowest = h;
            for (n : getNeighbors(self)) {
                oh = getHeight(n) + getFullAtt("attWater", n);
                if (oh < lowest) {
                    lowest = oh;
                    next = n;
                }
            }

            if (next) {
                d = floor( min( (h - lowest) / 2, ..attWater / 2 ) );
                if (d > 0) {
                    ..attWater -= d;
                    h -= d;

                    if (lowest > 100) {
                        other = getInstance(entWater, next);
                        if (other) {
                            other..attWater += d;
                        } else {
                            (create(entWater, getTile(next)))..attWater += d;
                        }
                    } else {
                        other = getInstance(entCloud, next);
                        if (other) {
                            other..attHumidity += 50;
                        } else {
                            (create(entCloud, getTile(next)))..attHumidity += 50;
                        }
                    }

                    if (getTile(self)..attSediment > 0) {
                        h1 = getHeight(self);
                        h2 = getHeight(next);
                        if (h1 > h2 + 1 && chance(getTile(self)..attSediment / 100)) {
                            h--;
                            getTile(self)..attSediment--;
                            next..attSediment++;
                            setHeight(self, h1 - 1);
                            setHeight(next, h2 + 1);
                        }
                    }
                }
            }
        }

        setWaterHeight(self, h);
	    updatePlanetMesh();

        if (..attWater <= 0) {
            destroy(self);
        }
    }
}


Entity: entCloud {
    name = "Cloud";
	mesh = "cloud.ply";
	opacity = 0.25;

	attributes {
	    attTicks, 20;
	}

    Script: tick {
        ..attTicks--;
        if (.attTicks > 0) {
            print("inside nr of ticks: " + .attTicks);
            return();
        }
        ..attTicks = entCloud.attTicks;
        print("outside nr of ticks: " + .attTicks);

        tile = getNeighbor(self, random(3));
        other = getInstance(entCloud, tile);
        if (other) {
            other..attHumidity += self..attHumidity;
            destroy(self);
            return();
        } else {
            moveTo(self, tile, 1);
        }

        if (..attHumidity > 100) {
            tile = getTile(self);
            other = getInstance(entWater, tile);
            if (other) {
                other..attWater += 1;
            } else {
                if (getHeight(tile) > getWaterHeight(tile)) {
                    (create(entWater, tile))..attWater += 1;
                }
            }
            ..attHumidity -= 100;
        }

        if (..attHumidity <= 0) {
            destroy(self);
        }
    }
}