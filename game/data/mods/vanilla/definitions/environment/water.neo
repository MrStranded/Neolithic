Entity: entSource {
    name = "Sweet water source";

    Script: tick {
        water = getInstanceInRange(entWater, self, 0);

        if (water) {
            water..attWater++;
        } else {
            (create(entWater, getTile(self)))..attWater++;
        }
    }
}

Entity: entWater {
    name = "Sweet water";

    Script: tick {
        if (chance(0.01)) {
            ..attWater--;
        }

        h = getHeight(self) + ..attWater;
        lowest = h;
        for (n : getNeighbors(self)) {
            oh = getHeight(n) + getFullAtt(n, "attWater");
            if (oh < lowest) {
                lowest = oh;
                next = n;
            }
        }

        if (next) {
            d = floor((h - lowest) / 2);
            if (d > 0) {
                ..attWater -= d;
                h -= d;

                if (lowest > 100) {
                    other = getInstanceInRange(entWater, next, 0);
                    if (other) {
                        other..attWater += d;
                    } else {
                        (create(entWater, getTile(next)))..attWater += d;
                    }
                }

                if (chance(0.2)) {
                    setHeight(self, getHeight(self) - 1);
                    setHeight(next, getHeight(next) + 1);
                }
            }
        }

        if (h != getWaterHeight(self)) {
            setWaterHeight(self, h);
            updatePlanetMesh();
        }

        if (..attWater <= 0) {
            destroy(self);
        }
    }
}