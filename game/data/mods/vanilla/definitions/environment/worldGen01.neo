WorldGen: genContinental {
	name = "Continental";

	Script: generate {
		setWaterLevel(100);
		print("water level set");

		for (t : eachTile()) {
			if (chance(0.0025)) { // 1/4% chance
				if (chance(0.5)) {
					destroy(create(formPlateau, t));
				} else if (chance(0.5)) {
					destroy(create(formMountain, t));
				} else {
					destroy(create(formHill, t));
				}
			}
		}

		for (t : eachTile()) {
			if (getHeight(t) > 120) {
				if (chance(0.005)) {
					create(entDeepSource, t);
				}
			}
		}

		fitTiles(tSand, tStone);
	    updatePlanetMesh();
	}
}

Formation: formMountain inherits formLift {
    attributes {
        attMinDeviation, 2; attMaxDeviation, 40;
    }

	Script: create(tile) {
		height = random(220,420);
		lift(self, height);

		if (chance(0.5)) {
		    create(entSource, getNeighbor(self, random(3)));
		}
	}

}

Formation: formHill inherits formLift {

	Script: create() {
		height = random(150,200);
		self->lift(self, height);
	}

}

Formation: formPlateau {
	Script: create() {
		height = random(110,230);
		if (height >= 170) { height = random(130,220);}
		distance = random(12,20);
		self->lift(self, height, distance);

		if (chance(0.5)) {
		    create(entSource, getNeighbor(self, random(3)));
		}
	}

	Script: lift(tile, height, distance) {
		if (chance(0.1)) {
			->lift(tile, height + random(5,15), distance / 2);
		} else if (height > getHeight(tile)) {
			setHeight(tile, height);

			for (i=0; i<3; i++) {
				if (distance - random(0,7) >= 0) {
					deviation = random(-1,3);
				} else {
					deviation = random(30,50);
				}
				lift(getNeighbor(tile, i), height - deviation, distance - 1);
			}
		}
	}
}

Formation: formMound inherits formLift {

    attributes {
        attMinDeviation, 1; attMaxDeviation, 20;
    }

	Script: create() {
	    ->lift(self, getHeight(self) + 50);
	    updatePlanetMesh();
	    destroy(self);
	}
}

Formation: formHole inherits formSink {

    attributes {
        attMinSink, 1; attMaxSink, 20;
    }

	Script: create() {
	    ->sink(self, getHeight(self) - 50);
	    updatePlanetMesh();
	    destroy(self);
	}
}

Formation: formLift {
    attributes {
        attMinDeviation, 1;
        attMaxDeviation, 12;
    }

	Script: lift(tile, height) {
		if (height > getHeight(tile)) {
			setHeight(tile, height);

			for (n : getNeighbors(tile)) {
				deviation = random(.attMinDeviation, .attMaxDeviation);
				lift(n, height - deviation);
			}
		}
	}
}
Formation: formSink {
    attributes {
        attMinSink, 1;
        attMaxSink, 12;
    }

	Script: sink(tile, height) {
		if (height < getHeight(tile)) {
			setHeight(tile, height);

			for (n : getNeighbors(tile)) {
				deviation = random(.attMinSink, .attMaxSink);
				sink(n, height + deviation);
			}
		}
	}
}

WorldGen: genDesert {
	name = "Desert";

	Script: generate {
		setWaterLevel(50);

		for (t : eachTile()) {
			setHeight(t, 51);
		}

		for (t : eachTile()) {
			if (chance(0.01)) {
				setHeight(t, 49);
			}
		}

		fitTiles();
	}
}