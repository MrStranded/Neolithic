
///////////////////////////////////////////////////////////////////////////// Effects

Entity: entShit {
    name = "Shit";
    mesh = "shit01.ply";

    Script: tick {
        if (isOnFloor(self)) {
            tile = getTile(self);
            if (getType(tile) == tDirt && .attNutrition >= 10) {
                ..attNutrition -= 10;
                change(tile, tGras);
            }

            addEffect(getTile(self), "Fertilized", 1, [ ..attNutrition ]);
            ..attNutrition--;

            if (.attNutrition <= 0) {
                destroy(self);
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////// Effects

Effect: effHunger {
    name = "Hunger";

    attributes {
        attHealth, "-1";
        attSpeed, "-1";
    }

    Script: tick (carrier) {
        if (!carrier.attSleeping) {
            if (carrier.attNutrition > 0) {
                if (.attHealth < 0) {
                    ..attHealth++;
                }
            } else {
                ..attHealth--;
                carrier..attFatigue++;
            }
        }
    }

    Script: removeCondition {
        require(.attHealth >= 0);
    }
}

Effect: effDigest {
    name = "Digest";

    Script: create() {
        ->shit = create(entShit, self);
    }

    Script: tick(carrier) {
        ..attNutrition -= 2;
        carrier..attNutrition++;
        ->shit..attNutrition++;
    }
    Script: removeCondition() { require(.attNutrition <= 0); }

    Script: finally(carrier) {
        tile = getTile(carrier);

        items = getItems(self);
        if (items) {
            for (item : items) {
                pickUp(tile, item);
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////// Drives

Drive: driHunger {
	Script: condition {
	    require(! .attSleeping);
	    if (.attNutrition <= 0 && !getEffects(self, effHunger)) {
	        addEffect(self, effHunger);
	    }
		require(.attNutrition <= .attHungerSensitivity);
	}

	solutions {
		proEat;
	}

	Script: getWeight() {
	    return(max(.attHungerSensitivity - .attNutrition, 0));
	}
}

Drive: driHording {
	Script: condition {
		require(! .attSleeping);
		require(getFullAtt(self, "attNutrition") < .attGreed);
	}

	solutions {
		proGatherFood;
	}

	Script: getWeight() {
		return(.attGreed - getFullAtt(self, "attNutrition"));
	}
}

///////////////////////////////////////////////////////////////////////////// Processes

Process: proEat {
	Script: condition {
		require(getItemAtt(self, "attEdible"));
	}
	Script: process {
		item = getItemAtt(self, "attEdible");
		item->consume(self);
	}

	solutions {
		proPickUpFood;
		proGatherFood;
		proBeg;
	}
}

Process: proPickUpFood {
	Script: condition {
	    ->food = getAttInRange("attEdible",self,0);
		require(->food);
	}
	Script: process() {
		pickUp(self, ->food);
	}
}

Process: proGatherFood {
	Script: condition {
		->food = getAttInRange("attEdible",self,.attViewingDistance);
		require(->food);
	}
	Script: process() {
		moveTo(self, ->food, .attSpeed);
		pickUp(self, ->food);
	}
	Script: discovery() {
		require(.attSpeed > 0);
		require(.attViewingDistance > 0);
	}
}

Process: proBeg {
	Script: condition {
		//print("trying to beg");
		require(->patron);
		require(isNeighbor(self, ->patron));

		->food = getItemAtt(->patron, "attEdible");
		require(->food);

		hunger = .attHungerSensitivity - .attNutrition;
		require(hunger > .attPride);
		require(hunger > ->patron.attGreed - ->patron.attPride);
	}

	Script: process {
		pickUp(self, ->food);
		->patron = false;
	}

	solutions {
		proSearchPatron;
	}
}

Process: proSearchPatron {
	Script: condition {
		//print("looking for patron");
		creatures = getInstancesInRange(getType(self), getTile(self), .attViewingDistance);
		->patron = false;
		for (creature : creatures) {
			if (creature != self && getItemAtt(creature, "attEdible")) {
				->patron = creature;
				break;
			}
		}
		require(->patron);
	}

	Script: process {
		//print("moving to patron");
		moveTo(self, ->patron, .attSpeed);
	}
}