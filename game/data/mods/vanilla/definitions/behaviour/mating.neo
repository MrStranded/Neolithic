
///////////////////////////////////////////////////////////////////////////// Effects

Effect: effInfant {
    name = "Infant";

    attributes {
        attViewingDistance, "-3";
        attSpeed, "-1";
    }

    Script: removeCondition(carrier) {
        require(carrier.attAge >= carrier.attMatureAge);
    }

    Script: finally(carrier) {
        carrier->updateMesh();
    }
}

///////////////////////////////////////////////////////////////////////////// Drives

Drive: driMate {
    Script: condition {
	    require(! .attSleeping);
        require(self.attNutrition >= .attBabySize);
        require(self.attFatigue < .attLibido);
        require(self.attAge >= self.attMatureAge);
        require(self.attGender <= 0); // only males want to fock
    }

    solutions {
        proMate;
    }

	Script: getWeight() {
	    return(max(.attAge - .attMatureAge, 0));
	}
}

///////////////////////////////////////////////////////////////////////////// Processes

Process: proMate {
    Script: condition {
        require(->mate);
    }
    Script: process {
        addEffect(self, "Postcoital exhaustion", 300, [
            "attFatigue", .attLibido
        ]);
        ..attNutrition -= .attBabySize;

        child = create(getType(self), getTile(self));

        mixAttributes(child, self, ->mate);
        addEffect(child, effInfant);
        child..attNutrition += .attBabySize;
    }

    solutions {
        proSearchMate;
    }
}

Process: proSearchMate {
    Script: condition {
        creatures = getInstancesInRange(getType(self), getTile(self), .attViewingDistance);
        ->mate = false;
        for (creature : creatures) {
            // males are all gender values <= 0, females are all gender values > 0. thus males*females <= 0 for heterogenous reproduction
            if (creature != self && creature.attAge >= creature.attMatureAge && self.attGender*creature.attGender < 0 && getType(creature) == getType(self)) {
                ->mate = creature;
                break;
            }
        }
        require(->mate);
    }
    Script: process {
        moveTo(self, ->mate, self.attSpeed);
    }
}
