
///////////////////////////////////////////////////////////////////////////// Effects

Effect: effInfant {
    name = "Infant";

    attributes {
        attViewingDistance, "-3";
        attSpeed, "-1";
    }

    Script: removeCondition(carrier) {
        require(carrier.attAge >= carrier.attMatureAge);
    }
}

///////////////////////////////////////////////////////////////////////////// Drives

Drive: driMate {
    Script: condition {
        require(self.attNutrition >= 69);
        require(self.attFatigue < 100);
        require(self.attAge >= self.attMatureAge);
    }

    solutions {
        proMate;
    }

	Script: getWeight() {
	    return(max(.attAge - .attMatureAge, 0));
	}
}

///////////////////////////////////////////////////////////////////////////// Processes

Process: proMate {
    Script: condition {
        require(->mate);
    }
    Script: process {
        addEffect(self, "Postcoital exhaustion", 300, [
            "attFatigue", 300
        ]);
        ..attNutrition -= 69;

        child = create(getType(self), getTile(self));
        mixAttributes(child, self, ->mate);
        addEffect(child, effInfant);
    }

    solutions {
        proSearchMate;
    }
}

Process: proSearchMate {
    Script: condition {
        creatures = getCreaturesInRange(getType(self), getTile(self), .attViewingDistance);
        ->mate = false;
        for (creature : creatures) {
            if (creature != self && creature.attAge >= creature.attMatureAge) {
                ->mate = creature;
                break;
            }
        }
        require(->mate);
    }
    Script: process {
        moveTo(self, ->mate, self.attSpeed);
    }
}
